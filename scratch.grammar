
<timex>                 ::= <span> | <frequency> | <date>
<date>                  ::= <spoken_date> | <relative_date>                                                     
<spoken_date>           ::= <day_of_week>                                                                       
<day_of_week>           ::= <day_of_week_long> | <day_of_week_short> <maybe_dot>
<relative_date>         ::= <relative_day> | <next_day_of_week> | <this_day_of_week> | <on_day_of_week>
<next_day_of_week>      ::= "next" <maybe_week> <day_of_week>
<this_day_of_week>      ::= "this" <maybe_week> <day_of_week>
<on_day_of_week>        ::= "on" <day_of_week>
<relative_day>          ::= "today" | "tomorrow" | "yesterday"
<maybe_week>            ::= "week" | ""

<frequency>             ::= <on_frequency> | <every_frequency> | <relative_frequency>
<on_frequency>          ::= "on" <day_of_week_long> "s"
<every_frequency>       ::= "every" <day_of_week>

<span>                  ::= <maybe_from> <date> <to_or_until> <date>
<maybe_from>            ::= "from" | ""
<to_or_until>           ::= "to" | "until" | "-"


<month_long>            ::= "january" | "february" | "march" | "april" | "may" | "june" | "july" | "august" | "september" | "october" | "november" | "december"
<month_short>           ::= "jan" | "feb" | "mar" | "apr" | "may" | "jun" | "jul" | "aug" | "sep" | "sept" | "oct" | "nov" | "dec"
<day_of_week_long>      ::= "monday" | "tuesday" | "wednesday" | "thursday" | "friday" | "saturday" | "sunday"  
<day_of_week_short>     ::= "mon" | "tue" | "wed" | "weds" | "thur" | "thu" | "thurs" | "fri" | "sat" | "sun"
<maybe_dot>             ::= "." | ""   